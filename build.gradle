buildscript {
    ext.kotlinVer = '1.0.0-beta-2423'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVer"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

version             = '1.0.0-SNAPSHOT'
status              = 'integration'

ext.classifier      = ':jdk8'
sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.quasarVer       = '0.7.4-SNAPSHOT'
ext.logbackVer      = '1.1.3'
ext.slf4jVer        = '1.7.12'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

configurations {
    quasar
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()

        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVer"
        force "org.slf4j:slf4j-api:$slf4jVer"
    }
}

repositories {
//    mavenLocal()
    mavenCentral()
    jcenter()
//    maven { url "https://oss.sonatype.org/content/repositories/releases" }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
//    maven { url 'https://maven.java.net/content/repositories/snapshots' }
}

dependencies {
    compile "co.paralleluniverse:quasar-core:${quasarVer}${classifier}"
    compile "co.paralleluniverse:quasar-kotlin:${quasarVer}"

    quasar "co.paralleluniverse:quasar-core:${quasarVer}${classifier}@jar"

    runtime "ch.qos.logback:logback-classic:${logbackVer}"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVer"

    compile 'io.reactivex:rxkotlin:0.30.1'
}

mainClassName = 'com.maarek.kosar.MainKt'

applicationDefaultJvmArgs = [
        "-javaagent:${configurations.quasar.singleFile}" // =v, =d
]

run {
    standardInput = System.in
}

defaultTasks 'run'
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}
